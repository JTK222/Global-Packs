plugins {
    id 'org.quiltmc.loom' version "0.12.+"
	id 'maven-publish'
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
}

archivesBaseName = "${mod_file_name}-quilt-${minecraft_version}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "org.quiltmc:quilt-loader:${quilt_loader_version}"
//    modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilt_version}"

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":Common")

    implementation group: 'com.electronwill.night-config', name: 'core', version: '3.6.5'
    implementation group: 'com.electronwill.night-config', name: 'toml', version: '3.6.5'

    include group: 'com.electronwill.night-config', name: 'core', version: '3.6.5'
    include group: 'com.electronwill.night-config', name: 'toml', version: '3.6.5'
}

loom {
    mixin {
        defaultRefmapName = "${mod_id}.refmap.json"
    }

    runs {
        client {
            client()
            setConfigName("Quilt Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Quilt Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    exclude "fabric.mod.json"

    filesMatching("quilt.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

curseforge{
    apiKey = project.findProperty('curse_auth')
    project{
        id = project.findProperty('curse_project')
        releaseType = project.findProperty('curse_release_type_quilt')
        changelogType = 'markdown'
        if(new File("../", "Changelog.md").exists())
            changelog = new File("../", "Changelog.md").text

        Arrays.asList(project.findProperty('curse_versions').toString().split(",")).forEach(version -> addGameVersion(version));
        addGameVersion 'Quilt'

        mainArtifact(remapJar) {
            displayName = "$project.ext.mod_name - Quilt - $project.ext.minecraft_version - v$project.version"
        }

        afterEvaluate{
            uploadTask.dependsOn(remapJar)
        }
        options{
            forgeGradleIntegration = false;
        }
    }
}

modrinth {
    token = project.findProperty('modrinth_auth')
    projectId = project.findProperty('modrinth_project')
    versionType = project.findProperty('modrinth_release_type_quilt')
    versionName = "$project.ext.mod_name - v${version}"
    version = "${version}_quilt"
    uploadFile = remapJar
    gameVersions = Arrays.asList(project.findProperty('modrinth_versions').toString().split(","))
    loaders = ["quilt"]

    if(new File("../", "Changelog.md").exists())
        changelog = new File("../", "Changelog.md").text
}

tasks.register('publishEverywhere', GradleBuild) {
    tasks = [
            'clean',
            'modrinth',
            'curseforge'
    ]
}